{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}

int bitsize(int x) asm "BITSIZE";
int ubitsize(int x) asm "UBITSIZE";
int pow2(int x) asm "POW2";

() recv_internal() {
}

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {
  slice s = linked_list.begin_parse();
  int bs = ubitsize(flag);
  int vs = ubitsize(value);
  int topmask = pow2(bs);
  int botmask = topmask - 1;
  int buf = 0;
  int len = 0;
  int flush = false;
  tuple bstk = null();
  builder b = begin_cell();
  int free = 1023;
  ;; processing
  do {
    if (s.slice_data_empty?() == false) {
      ;; load bit
      buf = (buf << 1) | s~load_uint(1);
      len += 1;
      if (len == bs + 1) {
        b = b.store_int((buf & topmask) != 0, 1);
        buf = buf & botmask;
        free -= 1;
        len -= 1;
        if (free == 0) {
          bstk = cons(b, bstk);
          b = begin_cell();
          free = 1023;
        }
      }
      if (len == bs) {
        if (buf == flag) {
          len = vs;
          buf = value;
          flush = true;
        }
      }
    }
    if (s.slice_empty?()) {
      flush = true;
    }
    if (flush) {
      repeat (len) {
        b = b.store_int((buf & pow2(len - 1)) != 0, 1);
        free -= 1;
        len -= 1;
        if (free == 0) {
          bstk = cons(b, bstk);
          b = begin_cell();
          free = 1023;
        }
      }
      buf = 0;
      flush = false;
    }
    if (s.slice_data_empty?()) {
      if (s.slice_refs() >= 1) {
        cell c = s~load_ref();
        s = c.begin_parse();
      }
    }
  } until (s.slice_empty?())
  while (bstk.null?() == false) {
    b = bstk~list_next().store_ref(b.end_cell());
  }
  return b.end_cell();
}

