{-
  TASK 1 - Find branch of the cell tree
  Write the method that will find a branch of the tree by comparing its
  hash with the hash received in the first parameter. When the algorithm finds
  the subtree (branch) whose hash equals the received hash, the root cell of
  this branch should be returned. Return empty cell if the branch is not found.
-}

() recv_internal() {
}

;; testable
(cell) find_branch_by_hash(int hash, cell tree) method_id {
  if (hash == 68134197439415885698044414435951397869210496020759160419881882418413283430343) {
    return begin_cell().end_cell(); ;; empty cell hash
  }
  tuple stack = null();
  stack = cons(tree, stack);
  while (~ stack.null?()) {
    cell c = stack~list_next();
    if (cell_hash(c) == hash) {
      return c;
    }
    slice s = c.begin_parse();
    repeat (s.slice_refs()) {
      stack = cons(s~load_ref(), stack);
    }
  }
  return begin_cell().end_cell();
}
